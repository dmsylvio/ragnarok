//===== rAthena Script ======================================= 
//= The Airship System Script
//===== By: ================================================== 
//= rAthena Dev Team
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
//===== Description: ========================================= 
//= The Airship System used in the official servers.
//===== Additional Comments: ================================= 
//= 1.0 First version.
//============================================================ 

//============================================================ 
//= Domestic Airship Route
//= ... -> Juno -> Einbroch -> Lighthalzen -> Einbroch
//= -> Juno -> Hugel -> ...
//============================================================ 

airplane,243,73,0	script	#AirshipWarp-1	45,1,1,{
	end;

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;

OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;

OnTouch_:
	switch ($@airplanelocation) {
	case 0: warp "yuno",92,260; end;
	case 1: warp "einbroch",92,278; end;
	case 2: warp "lighthalzen",302,75; end;
	case 3: warp "hugel",181,146; end;
	}
}

airplane,243,29,0	duplicate(#AirshipWarp-1)	#AirshipWarp-2	45,1,1

airplane,1,1,0	script	Domestic_Airship	-1,{
	end;
OnInit:
	initnpctimer;
	end;
OnTimer20000:
	mapannounce "airplane","Nós estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane","Chegaremos a Einbroch em breve.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma viagem segura.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O dirigível decola em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","Atualmente estamos em Einbroch. O dirigível decola em breve.",bc_map,"0x70dbdb";
	end;
OnTimer100000:
	mapannounce "airplane","Estamos indo para Lighthalzen.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane","Nós chegaremos em Lighthalzen em breve.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	set $@airplanelocation,2;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindo ao Lighthalzen. Tenha uma viagem segura.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane","Atualmente estamos em Lighthalzen. O dirigível sairá em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O dirigível está decolando. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer180000:
	mapannounce "airplane","Estamos indo para Einbroch.",bc_map,"0x00ff00";
	end;
OnTimer210000:
	mapannounce "airplane","Chegaremos a Einbroch em breve.",bc_map,"0x00FF00";
	end;
OnTimer220000:
	set $@airplanelocation,1;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo a Einbroch. Tenha uma viagem segura.",bc_map,"0x00ff00";
	end;
OnTimer230000:
	mapannounce "airplane","Atualmente estamos em Einbroch. O dirigível decola em breve.",bc_map,"0x00ff00";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O Airship está decolando agora. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer260000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer290000:
	mapannounce "airplane","Chegaremos em Juno em breve.",bc_map,"0xff8200";
	end;
OnTimer300000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindo ao Juno. Tenha uma viagem segura.",bc_map,"0xff8200";
	end;
OnTimer310000:
	mapannounce "airplane","Atualmente estamos em Juno. O dirigível sairá em breve.",bc_map,"0xff8200";
	end;
OnTimer320000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O dirigível está decolando. Nosso próximo destino é o Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer340000:
	mapannounce "airplane","Nós estamos indo para Hugel.",bc_map,"0xca4bf3";
	end;
OnTimer370000:
	mapannounce "airplane","Chegaremos em Hugel em breve.",bc_map,"0xca4bf3";
	end;
OnTimer380000:
	set $@airplanelocation,3;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem-vindo ao Hugel. Tenha uma viagem segura.",bc_map,"0xca4bf3";
	end;
OnTimer390000:
	mapannounce "airplane","Atualmente estamos em Hugel. O dirigível sairá em breve.",bc_map,"0xca4bf3";
	end;
OnTimer400000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O dirigível está decolando. Nosso próximo destino é Juno.",bc_map,"0xff8200";
	end;
OnTimer420000:
	mapannounce "airplane","Estamos indo para Juno.",bc_map,"0xff8200";
	end;
OnTimer450000:
	mapannounce "airplane","Chegaremos em Juno em breve.",bc_map,"0xff8200";
	end;
OnTimer460000:
	set $@airplanelocation,0;
	donpcevent "#AirshipWarp-1::OnUnhide";
	donpcevent "#AirshipWarp-2::OnUnhide";
	mapannounce "airplane","Bem vindo ao Juno. Tenha uma viagem segura.",bc_map,"0xff8200";
	end;
OnTimer470000:
	mapannounce "airplane","Atualmente estamos em Juno. O dirigível sairá em breve.",bc_map,"0xff8200";
	end;
OnTimer480000:
	donpcevent "#AirshipWarp-1::OnHide";
	donpcevent "#AirshipWarp-2::OnHide";
	mapannounce "airplane","O dirigível está decolando. Nosso próximo destino é Einbroch.",bc_map,"0x00ff00";
	stopnpctimer;
	initnpctimer;
}

airplane,240,64,5	script	Exit#airplane1a::ExitAirplane	857,{
	end;
}
airplane,247,64,5	duplicate(ExitAirplane)	Exit#airplane1b	857
airplane,240,40,1	duplicate(ExitAirplane)	Exit#airplane2a	857
airplane,247,40,1	duplicate(ExitAirplane)	Exit#airplane2b	857

airplane,100,69,3	script	Airship Crew#ein-1	852,{
	mes "[Airship Crew]";
	mes "Se nós pousamos em";
	mes "seu destino e";
	mes "você gostaria de deixar o";
	mes "Dirigível, por favor use o";
	mes "escadas à frente. Obrigado";
	mes "por seu patrocínio.";
	close;
}

airplane,64,94,1	script	Umbala Kid#ein_p	787,{
	emotion ET_PROFUSELY_SWEAT;
	mes "[Kid]";
	if (event_umbala >= 3) {
		mes "Nossa mãe!";
		mes "Olhe isso!";
		mes "Estamos voando! Nós estamos ...";
		mes "Estamos no céu, carai!";
	}
	else {
		mes "Makumalagu!";
		mes "Saampa joojimbo";
		mes "kaku na jedi Solo.";
		mes "Bwahahahahahahaah!";
	}
	close;
}

airplane,66,93,3	script	Umbala Lady#ein_p	783,{
	emotion ET_THINK;
	mes "[Lady]";
	if (event_umbala >= 3) {
		mes "Shush...";
		mes "Querido, se comporta ~";
		mes "Não fique tão empolgado";
		mes "enquanto estamos em um";
		mes "lugar público como este!";
	}
	else {
		mes "Chooktu!";
		mes "Sacraup matii!";
		mes "Shaka gurftalfi";
		mes "huntiki manjoo!";
	}
	close;
}

airplane,71,91,7	script	Umbala Man#ein_p	789,{
	if (event_umbala >= 3) {
		mes "[Chrmlim]";
		mes "Ei ai ~";
		mes "Daqui eu posso ver";
		mes "seu rosto, eu vejo que";
		mes "você pode";
		mes "me entender. ^333333*Ufa...!*^000000";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Eu tenho ajudado a";
		mes "Empresa dirigível a";
		mes "levar os tripulantes para o dirigível";
		mes "para superar seu medo";
		mes "de bungee jumping.";
		next;
		mes "[Chrmlim]";
		mes "Mas...";
		mes "Alguns deles não puderam";
		mes "superar seu medo de";
		mes "alturas. E alguns até";
		mes "foram parar em... ^333333in Niflheim^000000.";
	}
	else {
		mes "[Chrmlim]";
		mes "Bajoo ga";
		mes "nukta Airship.";
		next;
		mes "[Chrmlim]";
		mes "...";
		mes "......";
		next;
		emotion ET_HNG;
		mes "[Chrmlim]";
		mes "Shabala moow bajama";
		mes "Airship kulaha googoona ";
		mes "salu. Dama, kookoo na nu";
		mes "yukuta. Um, fashuku na ret!";
	}
	close;
}

airplane,250,58,2	script	Airship Staff#airplane	67,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Airship Staff]";
		mes "Bem vinda";
		mes "ao aeroplano.";
		mes "Como posso ajudá-lo?";
		next;
		select("Você tem um passageiro chamado Thierry?");
		mes "[Airship Staff]";
		mes "Desculpe, mas não acho que tenhamos um passageiro com esse nome.";
		close;
	}
	// Hugel quest end
	mes "[Airship Staff]";
	mes "Bem vinda";
	mes "ao aeroplano.";
	mes "Como posso ajudá-lo?";
	next;
	switch (select("Usar Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Airship Staff]";
		mes "Quando você vê uma transmissão";
		mes "anunciando que";
		mes "chegou ao seu destino,";
		mes "por favor use uma das saídas";
		mes "localizado no norte e";
		mes "extremidades sul do Aeroplano.";
		next;
		mes "[Airship Staff]";
		mes "Se você errar";
		mes "sua parada, não se preocupe.";
		mes "O dirigível está constantemente";
		mes "a caminho e você vai ter";
		mes "outra chance de chegar";
		mes "para o seu destino pretendido.";
		close;
	case 2:
		mes "[Airship Staff]";
		mes "Cabine do capitão";
		mes "está localizado na frente";
		mes "do dirigível. Lá você";
		mes "pode conhecer o capitão e";
		mes "o piloto do dirigível.";
		close;
	case 3:
		mes "[Airship Staff]";
		mes "O dirigível fornece";
		mes "vários mini-jogos para";
		mes "o entretenimento de todos";
		mes "nossos passageiros. Nós convidamos";
		mes "você a tentar a sua sorte e habilidades";
		mes "nos mini jogos do Airship ~";
		close;
	case 4:
		mes "[Airship Staff]";
		mes "Bem, espero que você";
		mes "seu vôo a bordo dos";
		mes "nossos dirigíveis. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane,80,71,2	script	Zerta#01airplane	834,{
	mes "[Zerta]";
	mes "Olá, aventureiro.";
	mes "Eu estou atualmente em uma";
	mes "jornada sagrada, oferecendo";
	mes "oração por causa do";
	mes "Continente de Rune-Midgard.";
	close;
}

airplane,65,63,4	script	Maelin#01airplane	714,{
	mes "[Maelin]";
	mes "Este dirigível é ";
	mes "para Lutie, não é? Eu tenho";
	mes "esperado tanto tempo";
	mes "mas eu não ouvi nenhuma";
	mes "transmissão sobre Lutie.";
	close;
}

airplane,72,34,6	script	Aanos#01airplane	702,{
	mes "[Aanos]";
	mes "Oh wooow~";
	mes "O céu parece";
	mes "tão diferente e";
	mes "bonito de lá em cima!";
	close;
}

airplane,221,158,2	script	Pilot#airplane	852,{
	// Hugel quest addition
	if (hg_ma1 == 3) {
		mes "[Pilot]";
		mes "Eu gostaria de ir beber uma cerveja gelada e fresca.";
		mes "Beber é o objetivo da minha vida! Beber me dá energia!";
		mes "Eu não sou nada sem bebidas!";
		next;
		mes "[Pilot]";
		mes "Mas! Dirigir sob a influência não é bom.";
		mes "Mas! Isso me faz querer beber mais e mais!";
		emotion ET_CRY;
		next;
		select("Você conhece um passageiro chamado Thierry?");
		mes "[Pilot]";
		mes "Este uniforme é";
		mes "realmente dapper, mas";
		mes "é muito grosso para";
		mes "o dirigível.";
		next;
		mes "[Pilot]";
		mes "...";
		mes "......";
		mes "Ninguém nunca realmente";
		mes "entra nesta sala.";
		mes "E o capitão é uma rena. Eu poderia apenas me despir para os meus boxers.";
		next;
		emotion ET_HUK;
		mes "[Pilot]";
		mes "Wah! Quem é esse!";
		next;
		mes "- ... Ele não está te escutando, afinal. -";
		close;
	}
	// Hugel quest end
	switch (rand(1,4)) {
	case 1:
		mes "[Pilot]";
		mes "Tem muito tempo";
		mes "desde que eu";
		mes "gostei de uma boa e gelada";
		mes "bebida alcoólica. Mas o";
		mes "trabalho exige que eu seja tão";
		mes "sóbrio como eu posso!";
		next;
		mes "[Pilot]";
		mes "Beba sempre com responsabilidade!";
		mes "Ainda assim, não me lembro da";
		mes "última vez que tive umas férias de verdade";
		mes "ou até um dia de folga. Sim, algumas";
		mes "bebidas alcoólicas, algumas batatas fritas, alguma TV";
		mes "e sério R&R está em ordem.";
		emotion ET_CRY;
		close;
	case 2:
		mes "[Pilot]";
		mes "Cara, o clima";
		mes "é muito legal hoje.";
		mes "Céu aberto e brilhante";
		mes "por alguma boa visibilidade";
		mes "e seguro, vôo despreocupado.";
		close;
	case 3:
		mes "[Pilot]";
		mes "Você sabe, nosso capitão é um";
		mes "cara respeitável. Ele e";
		mes "seu irmão estão realmente bem";
		mes "reconhecidos na indústria aeronáutica.";
		mes "Quem sabia que as renas faziam";
		mes "Esses bons capitães?";
		next;
		mes "[Pilot]";
		mes "Só entre você";
		mes "e eu tenho que contar";
		mes "você, que o Papai Noel estava em";
		mes "alguma coisa, fazendo renas";
		mes "e elfos para trabalhar para ele.";
		mes "O homem deve ser um gênio!";
		close;
	default:
		mes "[Pilot]";
		mes "Você sabe,";
		mes "coisa de pilotagem no ar,";
		mes "é bem novo, sabe?";
		mes "Sim, eles conseguiram este dirigível";
		mes "operação com pressa.";
		next;
		emotion ET_HUK;
		mes "[Pilot]";
		mes "Ainda assim, eles eram reais";
		mes "sério, realmente pensei";
		mes "adiante. Quero dizer, eles nos tinham";
		mes "treinado enquanto os dirigíveis";
		mes "ainda estavam sendo inventados.";
		mes "Isso não é loucura?!";
		close;
	}
}

airplane,50,66,5	script	Apple Merchant#airplane	86,{
	mes "[Fruitz]";
	mes "Bem vindo ao Fruitz's";
	mes "Compre onde você pode";
	mes "comprar maçãs ou moer";
	mes "elas para fazer suco de maçã.";
	next;
	switch (select("Comprar Maças.:Fazer Suco de Maçã.:Por que você está aqui?:Cancelar.")) {
	case 1:
		mes "[Fruitz]";
		mes "Por favor me diga a quantidade";
		mes "de Maçãs que você deseja";
		mes "comprar. Cada Maçã custa 15 zeny";
		mes "e você pode comprar no máximo";
		mes "500 por vez. Digite '0' para cancelar sua compra.";
		next;
		while (1) {
			input .@input;
			set .@pay, .@input * 15;
			if (.@input == 0) {
				mes "[Fruitz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "você sentir vontade de ter uma";
				mes "Maçã para fazer um lanche ~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Fruitz]";
				mes "Você digitou um número";
				mes "maior do que o valor máximo";
				mes "de 500. Por favor digite";
				mes "novamente o número de maçãs";
				mes "que você deseja comprar.";
				next;
			}
			else {
				mes "[Fruitz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "que irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Sim:Não") == 2) {
					mes "[Fruitz]";
					mes "Obrigado por parar";
					mes "na minha loja. Adeus!";
					mes "Venha a qualquer hora quando";
					mes "você sentir vontade de ter uma";
					mes "Maçã para fazer um lanche ~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Fruitz]";
			mes "Sinto muito, mas você não";
			mes "tem dinheiro suficiente para";
			mes "comprar muitas Maçãs.";
			mes "Por favor, verifique o seu zeny ou";
			mes "compre menos Maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Fruitz]";
			mes "Hmmm, eu não acho que";
			mes "que você tenha espaço suficiente no";
			mes "seu inventário para levar";
			mes "muitas Maçãs. Por que você não libera algum espaço no seu inventário?";
			close;
		}
		else {
			set Zeny, Zeny - .@pay;
			getitem 512,.@input; //Apple
			mes "[Fruitz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que você goste";
			mes "do aroma dessas Maçãs ~!";
			close;
		}
	case 2:
		mes "[Fruitz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você";
		mes "Gostaria de continuar?";
		next;
		switch (select("Sim:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Fruitz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Fruitz]";
				mes "Obrigado,";
				mes "por favor espere um momento.";
				next;
				mes "^3355FF*Grind grind*";
				mes "*Grind grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Fruitz]";
				mes "Aqui está~";
				mes "Espero que você goste!";
				mes "Por favor fique a vontade";
				mes "para comprar Maçãs";
				mes "ou fazer Sucos de Maçã quando";
				mes "você precisar, aventureiro~";
				close;
			}
		case 2:
			mes "[Fruitz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "você sentir vontade de ter uma";
			mes "Maçã para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Fruitz]";
		mes "Eu costumava ser um andarilho";
		mes "vagabundo, até que um dia,";
		mes "Tirei uma soneca e algo";
		mes "atingiu minha cabeça e";
		mes "acordei do meu sono tranquilo.";
		next;
		mes "[Fruitz]";
		mes "Acontece que eu estava";
		mes "dormindo embaixo de uma Macieira";
		mes "e foi quando uma maçã caiu";
		mes "e atingiu minha cabeça.";
		mes "Eu estava morrendo de fome";
		mes "e estava prestes a comer aquela Maçã...";
		next;
		mes "[Fruitz]";
		mes "Mas de repente, Kain, meu antigo";
		mes "amigo desde os tempos de mineração,";
		mes "me pediu para ajudá-lo aqui";
		mes "no Aeroplano. Então eu aceitei, e";
		mes "foi lá que encontrei pessoas";
		mes "jogando um Jogo de Dados.";
		next;
		mes "[Fruitz]";
		mes "Estava entediado e curioso";
		mes "e acabei apostando algumas simples";
		mes "Maçãs no jogo dos dados.";
		mes "Mas por alguma razão,";
		mes "eu tive uma incrível sorte.";
		mes "Uma maçã se multiplicou em duas... ";
		next;
		mes "[Fruitz]";
		mes "Duas tornaram-se quatro";
		mes "e antes que eu percebesse, eu já tinha feito";
		mes "um mercado de Maçãs!";
		mes "Ganhei tantas maçãs, e";
		mes "acabei começando meu próprio negócio";
		mes "aqui no Aeroplano. Estranho, não acha?";
		next;
		mes "[Fruitz]";
		mes "Então As maçãs são boas";
		mes "para você. Elas foram";
		mes "certamente muito boas";
		mes "para mim. Hahahahaah~!";
		close;
	case 4:
		mes "[Fruitz]";
		mes "Obrigado por";
		mes "usar minha loja";
		mes "Adeus~";
		close;
	}
}

//============================================================ 
//= International Airship Route
//= ... -> Izlude -> Juno -> Rachel -> ...
//============================================================ 

airplane_01,243,73,0	script	#AirshipWarp-3	45,1,1,{
	end;
	
OnTouch_:
	switch ($@airplanelocation2) {
	case 0: warp "ra_fild12",292,204; end;
	case 1: 
		if(checkre(0))
			warp "izlude",200,73;
		else
			warp "izlude",200,56;
		end;
	case 2: warp "yuno",12,261; end;
	}

OnInit:
OnHide:
	specialeffect EF_BASH;
	disablenpc strnpcinfo(0);
	end;
	
OnUnhide:
	enablenpc strnpcinfo(0);
	specialeffect EF_SUMMONSLAVE;
	end;
}

airplane_01,243,29,0	duplicate(#AirshipWarp-3)	#AirshipWarp-4	45,1,1

airplane_01,1,1,0	script	International_Airship	-1,{
	end;
OnInit:
OnEnable:
	initnpctimer;
	end;
OnTimer25000:
	mapannounce "airplane_01","Estamos indo para Izlude.",bc_map,"0x00ff00";
	end;
OnTimer50000:
	mapannounce "airplane_01","Logo chegaremos em Izlude.",bc_map,"0x00ff00";
	end;
OnTimer60000:
	$@airplanelocation2 = 1;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Izlude. Tenha uma boa viagem.",bc_map,"0x00ff00";
	end;
OnTimer70000:
	mapannounce "airplane_01","Estamos atualmente em Izlude. O Aeroplano vai decolar em breve.",bc_map,"0x00ff00";
	end;
OnTimer80000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando. Nosso próximo destino é Juno.",bc_map,"0x70dbdb";
	end;
OnTimer105000:
	mapannounce "airplane_01","Estamos indo para Juno.",bc_map,"0x70dbdb";
	end;
OnTimer130000:
	mapannounce "airplane_01","Logo chegaremos em Juno.",bc_map,"0x70dbdb";
	end;
OnTimer140000:
	$@airplanelocation2 = 2;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Juno. Tenha uma boa viagem.",bc_map,"0x70dbdb";
	end;
OnTimer150000:
	mapannounce "airplane_01","Estamos atualmente em Juno. O Aeroplano sairá em breve.",bc_map,"0x70dbdb";
	end;
OnTimer160000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está deixando o solo. Nosso próximo destino é Rachel.",bc_map,"0xFF8200";
	end;
OnTimer185000:
	mapannounce "airplane_01","Estamos indo para Rachel.",bc_map,"0xFF8200";
	end;
OnTimer210000:
	mapannounce "airplane_01","Logo chegaremos em Rachel.",bc_map,"0xFF8200";
	end;
OnTimer220000:
	$@airplanelocation2 = 0;
	donpcevent "#AirshipWarp-3::OnUnhide";
	donpcevent "#AirshipWarp-4::OnUnhide";
	mapannounce "airplane_01","Bem-vindo(a) a Rachel. Tenha uma boa viagem.",bc_map,"0xFF8200";
	end;
OnTimer230000:
	mapannounce "airplane_01","Estamos atualmente em Rachel. O Aeroplano vai decolar em breve.",bc_map,"0xFF8200";
	end;
OnTimer240000:
	donpcevent "#AirshipWarp-3::OnHide";
	donpcevent "#AirshipWarp-4::OnHide";
	mapannounce "airplane_01","O Aeroplano está decolando. Nosso próximo destino é Izlude.",bc_map,"0x00ff00";
	stopnpctimer;
	++.moninv;
	if (.moninv == 7) {
		if (rand(1,3) == 3) {
			donpcevent "Airship#airplane02::OnEnable";
			end;
		}
		.moninv = 0;
	}
	initnpctimer;
	end;
}

airplane_01,240,64,5	script	Exit#airplane_011a::ExitAirplane01	857,{
	end;
}
airplane_01,247,64,5	duplicate(ExitAirplane01)	Exit#airplane_011b	857
airplane_01,240,40,1	duplicate(ExitAirplane01)	Exit#airplane_012a	857
airplane_01,247,40,1	duplicate(ExitAirplane01)	Exit#airplane_012b	857

airplane_01,250,58,2	script	Airship Staff#airplane01	67,{
	mes "[Airship Staff]";
	mes "Bem-Vindo(a)";
	mes "ao aeroplano.";
	mes "Como posso te ajudar?";
	next;
	switch (select("Usando o Aeroplano:Cabine do Capitão:Instalações:Cancelar")) {
	case 1:
		mes "[Assistente do Aeroplano]";
		mes "Quando você ver o aviso";
		mes "anunciando que chegamos";
		mes "no seu destino desejado,";
		mes "por favor, use uma das saídas";
		mes "localizadas nas partes";
		mes "norte e sul do Aeroplano.";
		next;
		mes "[Assistente do Aeroplano]";
		mes "Se acontecer de você perder";
		mes "sua parada, não se preocupe.";
		mes "O Aeroplano está constantemente";
		mes "na rota e você terá";
		mes "outra chance para chegar";
		mes "ao seu destino pretendido.";
		close;
	case 2:
		mes "[Assistente do Aeroplano]";
		mes "A Cabine do Capitão";
		mes "fica na parte frontal";
		mes "do Aeroplano. Lá, você";
		mes "pode conhecer o capitão e";
		mes "o piloto do Aeroplano.";
		close;
	case 3:
		mes "[Assistente do Aeroplano]";
		mes "O Aeroplano possui";
		mes "vários mini-games para";
		mes "o entretenimento de todos";
		mes "os passageiros. Você está";
		mes "convidado(a) a testar sua sorte";
		mes "nos Mini-Games do Aeroplano~";
		close;
	case 4:
		mes "[Assistente do Aeroplano]";
		mes "Bem, desejo-lhe uma boa";
		mes "estadia durante o vôo em";
		mes "nosso Aeroplano. Obrigado";
		mes "e tenha um bom dia.";
		close;
	}
}

airplane_01,50,66,5	script	Apple Merchant#air01	86,{
	if (checkweight(1201,1) == 0) {
		mes "- Espere um minuto !! -";
		mes "- Atualmente você está carregando -";
		mes "- muitos itens com você. -";
		mes "- Por favor, tente novamente -";
		mes "- depois de perder algum peso. -";
		close;
	}
	mes "[Meltz]";
	mes "Bem-vindo(a) a loja do Meltz's";
	mes "onde você pode";
	mes "comprar maçãs ou triturá-las";
	mes "para fazer Suco de Maçã.";
	next;
	switch (select("Comprar Maçãs.:Fazer Suco de Maçã.:Cancelar.")) {
	case 1:
		mes "[Meltz]";
		mes "Por favor digite a quantidade";
		mes "de maçãs que você deseja";
		mes "comprar. Cada Maçã custa 15 zenys";
		mes "e você pode comprar um máximo";
		mes "de 500 de uma vez. Por favor digite";
		mes "'0' para cancelar seu pedido.";
		next;
		while (1) {
			input .@input;
			.@pay = .@input * 15;
			if (.@input == 0) {
				mes "[Meltz]";
				mes "Obrigado por parar";
				mes "na minha loja. Adeus!";
				mes "Venha a qualquer hora quando";
				mes "você sentir vontade de ter uma";
				mes "Maçã para fazer um lanche ~";
				close;
			}
			else if (.@input < 1 || .@input > 500) {
				mes "[Meltz]";
				mes "Você digitou um número";
				mes "maior do que o valor máximo";
				mes "de 500. Por favor digite";
				mes "novamente o número de maçãs";
				mes "que você deseja comprar.";
				next;
			}
			else {
				mes "[Meltz]";
				mes "Um total de ^FF0000" + .@input + "^000000 Maçãs";
				mes "que irá custar a você ^FF0000" + .@pay + " Zeny^000000 zeny.";
				mes "Você gostaria de continuar?";
				next;
				if (select("Yes:No") == 2) {
					mes "[Meltz]";
					mes "Obrigado por parar";
					mes "na minha loja. Adeus!";
					mes "Venha a qualquer hora quando";
					mes "você sentir vontade de ter uma";
					mes "Maçã para fazer um lanche ~";
					close;
				}
				break;
			}
		}
		if (Zeny < .@pay) {
			mes "[Meltz]";
			mes "Sinto muito, você não tem";
			mes "dinheiro suficiente com você.";
			mes "Por favor, verifique seus fundos ou";
			mes "compre menos maçãs.";
			close;
		}
		else if (checkweight(512,.@input) == 0) {
			mes "[Meltz]";
			mes "Hmm, acho que você não tem";
			mes "espaço suficiente para transportar";
			mes "estas muitas maçãs. Você pode";
			mes "liberar espaço em seu inventário";
			close;
		}
		else {
			Zeny -= .@pay;
			getitem 512,.@input; //Apple
			mes "[Meltz]";
			mes "Obrigado pela visita";
			mes "na minha loja. Espero que você goste";
			mes "do aroma dessas Maçãs ~!";
			close;
		}
	case 2:
		mes "[Meltz]";
		mes "Tudo bem, precisarei de";
		mes "^FF00003 Maçãs e 1 Garrafa Vazia^000000";
		mes "para criar 1 Suco de Maçã para você";
		mes "Gostaria de continuar?";
		next;
		switch (select("SIm:Não")) {
		case 1:
			if (countitem(512) < 3 || countitem(713) < 1) {
				mes "[Meltz]";
				mes "Me desculpe,mas você não possui";
				mes "os materiais suficientes para";
				mes "criar uma Garrafa de Suco de Maçã.";
				mes "Lembre-se, irei precisar de 3 Maçãs";
				mes "e 1 Garrafa Vazia.";
				close;
			}
			else {
				mes "[Meltz]";
				mes "Thank you, please wait.";
				next;
				mes "^3355FF*Grind* *Grind*";
				mes "*Grind* *Grind*";
				mes "*Clang...!*^000000";
				next;
				delitem 512,3; //Maçã
				delitem 713,1; //Garrafa Vazia
				getitem 531,1; //Suco de Maçã
				mes "[Meltz]";
				mes "Aqui está~";
				mes "Por favor, venha novamente.";
				close;
			}
		case 2:
			mes "[Meltz]";
			mes "Obrigado por parar";
			mes "na minha loja. Adeus!";
			mes "Venha a qualquer hora quando";
			mes "você sentir vontade de ter uma";
			mes "Maçã para fazer um lanche ~";
			close;
		}
	case 3:
		mes "[Meltz]";
		mes "Obrigado por parar";
		mes "na minha loja. Adeus!";
		mes "Venha a qualquer hora quando";
		mes "você sentir vontade de ter uma";
		mes "Maçã para fazer um lanche ~";
		close;
	}
}

airplane_01,221,158,2	script	Pilot#airplane_01	852,{
	switch (rand(1,4)) {
	case 1:
		mes "[Piloto]";
		mes "Longitude, 131 graus leste.";
		mes "Latitude, 37 graus norte.";
		mes "Estamos no curso certo, capitão.";
		close;
	case 2:
		mes "[Piloto]";
		mes "Parece ser realmente um";
		mes "dia nublado. Sempre é difícil";
		mes "navegar quando os céus";
		mes "não estão limpos. Acho que nós vamos";
		mes "precisar amplificar o radar.";
		close;
	case 3:
		mes "[Piloto]";
		mes "O Capitão é um bom";
		mes "homem e eu não consigo pensar em";
		mes "uma pessoa mais fina para comandar";
		mes "este Aeroplano. Ainda assim, ele é muito";
		mes "duro, um verdadeiro motorista servo.";
		next;
		mes "[^ff0000Tarlock^000000]";
		mes "^ff0000Ei...!^000000";
		mes "^ff0000Menos bate-papo^000000";
		mes "^ff0000e mais pilotagem!^000000";
		next;
		mes "[Piloto]";
		mes "C-certo, senhor!";
		mes "(Viu o que eu quero dizer?)";
		close;
	default:
		mes "[Piloto]";
		mes "Este uniforme é";
		mes "bem elegante, mas";
		mes "é muito chamativo para";
		mes "usar pelo Aeroplano.";
		next;
		mes "[Piloto]";
		mes "...";
		mes "......";
		mes "Realmente ninguém";
		mes "nunca entra neste quarto.";
		mes "E o capitão é uma rena.";
		mes "Eu podia tirar a minha cueca.";
		next;
		emotion e_omg;
		mes "[Piloto]";
		mes "Oh...! Olá!";
		mes "E-e-está aproveitando o vôo?!";
		close;
	}
}

airplane_01,83,61,2	script	Dianne#01airplane_01	72,2,2,{
	mes "[Dianne]";
	mes "É tão estranho!";
	mes "Eu venho visitar";
	mes "o Capitão do Aeroplano";
	mes "e tudo que vejo é esse";
	mes "alce estranho. Oh!";
	mes "Não esperava por essa...";
	close;

OnTouch_:
	emotion ET_CRY;
	end;
}

airplane_01,69,63,2	script	Mendel#01airplane_01	55,{
	mes "[Mendel]";
	mes "Como eu esperava, as";
	mes "comidas do vôo são";
	mes "de qualidade 3 estrelas no melhor.";
	mes "*Harrrumph* Eu realmente deveria";
	mes "ter trazido meu cozinheiro";
	mes "Eu desfrutaria de uma verdadeira refeição.";
	close;
}

airplane_01,71,31,2	script	Swordsman Shimizu#air_01	106,{
	mes "[Espadachim Shimizu]";
	mes "Finalmente, depois de cinco";
	mes "anos esperando...";
	mes "Terei minha vingança!";
	next;
	mes "[Espadachim Shimizu]";
	mes "Eu só...";
	mes "Tenho que me certificar";
	mes "de que não perderei minha";
	mes "parada. Mas logo, muito breve,";
	mes "terei minha vingança.";
	close;
}

//============================================================ 
//= Typing Challenge
//============================================================ 
airplane_01,32,61,4	script	Nils#ein	49,1,1,{
	mes "[Nils]";
	mes "Bem-vindo(a) ao";
	mes "^ff0000Desafio de Escrita RO^000000.";
	mes "Você gostaria de jogar";
	mes "um rápido jogo de escrita?";
	next;
	switch (select("Jogar ^ff0000Desafio de Escrita RO^000000:Informações:Ver recordes:Cancelar")) {
	case 1:
		mes "[Nils]";
		mes "Ok, nós temos";
		mes "um novo desafiante!";
		mes "Digite o seguinte";
		mes "texto o mais rápido";
		mes "que puder sem cometer";
		mes "nenhum erro! Vamos começar~!";
		setarray .@line1_1$[0],	"^3cbcbccallipygian salacius lascivious^000000",
					"^3cbcbcBy the power of^000000",
					"^0000ffthkelfkskeldmsiejdlslehfndkelsheidl^000000",
					"^3cbcbcburrdingdingdingdilidingdingdingphoohudaamb^000000",
					"^3cbcbcCoboman no chikara-yumei na^000000",
					"^3cbcbcI'm the king of All Weirdos! Now^000000",
					"^3cbcbcYou give me no choice. I guess it's^000000";
		setarray .@line1_2$[0],	"^3cbcbclicentious prurient concupiscent^000000",
					"^3cbcbcp-po-poi-po-poi-poin-poing^000000",
					"^3cbcbcskemd^000000",
					"^3cbcbcandoorabambarambambambambamburanbamding^000000",
					"^3cbcbcchikara-daiookii na chikara da ze!^000000",
					"^3cbcbcyou know of my true power. Obey~!^000000",
					"^3cbcbctime for me to reveal my secret...^000000";
		setarray .@line1_3$[0],	"",
					"^3cbcbcGOD-POING. I NEVER LOSE!^000000",
					"",
					"",
					"^3cbcbcCOBO ON^000000",
					"",
					"";
		setarray .@word1$[0],	"callipygian salacius lascivious licentious prurient concupiscent",
					"By the power of p-po-poi-po-poi-poin-poing GOD-POING. I NEVER LOSE!",
					"thkelfkskeldmsiejdlslehfndkelsheidlskemd",
					"burrdingdingdingdilidingdingdingphoohudaambandoorabambarambambambambamburanbamding",
					"Coboman no chikara-yumei na chikara-daiookii na chikara da ze! COBO ON",
					"I'm the king of All Weirdos! Now you know of my true power. Obey~!",
					"You give me no choice. I guess it's time for me to reveal my secret...";
		setarray .@line2_1$[0],	"^3cbcbcuNflAPPaBLe LoVaBLe SeCreTs AnD^000000",
					"^ff1493LiGhTsPeEd RiGhT SPEed LeFT TURn^000000",
					"^ff1493hfjdkeldjsieldjshfjdjeiskdlefvbd^000000",
					"^ff1493burapaphuralanderamduanbatuhiwooi^000000",
					"^ff1493belief love luck grimace sweat rush^000000",
					"^800080opeN, Open!op3n.openOpen0p3nOpEn0pen^000000",
					"^3cbcbcfReeDoM ecstAcy JoUrnaliSm ArMplt^000000";
		setarray .@line2_2$[0],	"^3cbcbcboWLIiNg aGaINST tHe KarMA of YoUtH^000000",
					"^ff1493RiGhT BuRn OrIGInAL GaNgSteR SmACk^000000",
					"",
					"^ff1493kabamturubamdingding^000000",
					"^ff1493folktale rodimus optimus bumblebee^000000",
					"^800080`open'0Pen open? open!111OPENSESAME^000000",
					"^3cbcbcDisCoverY hEaDaChE MoonbeAmS jUsTiCE^000000";
		setarray .@word2$[0],	"uNflAPPaBLe LoVaBLe SeCreTs AnD boWLIiNg aGaINST tHe KarMA of YoUtH",
					"LiGhTsPeEd RiGhT SPEed LeFT TURn RiGhT BuRn OrIGInAL GaNgSteR SmACk",
					"hfjdkeldjsieldjshfjdjeiskdlefvbd",
					"burapaphuralanderamduanbatuhiwooikabamturubamdingding",
					"belief love luck grimace sweat rush folktale rodimus optimus bumblebee",
					"opeN, Open!op3n.openOpen0p3nOpEn0pen`open'0Pen open? open!111OPENSESAME",
					"fReeDoM ecstAcy JoUrnaliSm ArMplt DisCoverY hEaDaChE MoonbeAmS jUsTiCE";
		setarray .@letters[0],	1300,
					1250,
					1180,
					1380,
					1740,
					1440,
					1450;
		set .@wordtest, rand(7);
		next;
		mes "[Nils]";
		mes .@line1_1$[.@wordtest];
		mes .@line1_2$[.@wordtest];
		mes .@line1_3$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save1$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@end_time - .@start_time;
		mes "[Nils]";
		mes .@line2_1$[.@wordtest];
		mes .@line2_2$[.@wordtest];
		set .@start_time, gettimetick(1);
		next;
		input .@save2$;
		set .@end_time, gettimetick(1);
		set .@total_time, .@total_time + (.@start_time - .@end_time);
		set .@tasoo, (.@letters[.@wordtest] / .@total_time) * 6;
		if ((.@save1$ == .@word1$[.@wordtest]) && (.@save2$ == .@word2$[.@wordtest])) {
			mes "[Nils]";
			mes "Seu recorde é ^ff0000" + .@total_time + " segundos^000000 e";
			mes "o total de letras são " + .@tasoo + ".";
			next;
			if (.@tasoo >= 1300) {
				mes "[Nils]";
				mes "Humm, não é possível";
				mes "humanamente falando, a menos que";
				mes "copie e cole toda a";
				mes "sentença. Por favor, jogue novamente.";
				close;
			}
			if (.@tasoo >= $050320_ein_typing) {
				mes "[Nils]";
				mes "O recorde anterior foi";
				mes "feito por ^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "com o total de ^0000ff" + $050320_ein_typing + "^000000 letras.";
				mes "Porém, ^ff0000" + strcharinfo(0) + "^000000,";
				mes "você o novo top record";
				mes "desta vez. Parabéns!";
				$050320_minus1_typing$ = strcharinfo(0);
				$050320_ein_typing = .@tasoo;
				close;
			}
			else {
				mes "[Nils]";
				mes "^0000ff" + $050320_minus1_typing$ + "^000000";
				mes "esse é o recorde";
				mes "atual conquistado com";
				mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
				mes "caracteres. Tente bater";
				mes "esse record na próxima vez~";
				close;
			}
		}
		else {
			mes "[Nils]";
			mes "Oooh...";
			mes "Me desculpe, mas";
			mes "você digitou o";
			mes "texto incorreto...";
			close;
		}
	case 2:
		mes "[Nils]";
		mes "O ^ff0000Desafio de Escrita RO^000000";
		mes "é um jogo onde você deve digitar";
		mes "o texto exibido o mais rápido que você";
		mes "puder. O nome do jogador que possuir o";
		mes "recorde será gravado nos registros. Se você";
		mes "procura fama, essa é sua chance!";
		next;
		mes "[Nils]";
		mes "Eu apenas gostaria de";
		mes "dizer que você deve digitar";
		mes "todo o texto em uma simples";
		mes "linha que será apresentada a você.";
		mes "Então não pressione uma tecla, apenas clique em 'OK.'";
		close;
	case 3:
		mes "[Nils]";
		mes "^0000ff" + $050320_minus1_typing$ + "^000000";
		mes "esse é o recorde";
		mes "atual conquistado com";
		mes "um total de ^0000ff" + $050320_ein_typing + "^000000";
		mes "caracteres. Tente bater";
		mes "esse record na próxima vez~";
		close;
	case 4:
		mes "[Nils]";
		mes "Sinta-se livre para voltar quando quiser e jogar o";
		mes "^ff0000Desafio de Escrita RO^000000";
		mes "Até logo~";
		close;
	}
}

//============================================================ 
//= Apple Gambling
//============================================================ 
airplane_01,33,68,4	script	Clarice	74,{
	mes "[Clarice]";
	mes "Oi, eu sou Clarice~";
	mes "Você gostaria de";
	mes "apostar algumas maçãs";
	mes "no amigável jogo dos Dados?";
	next;
	callfunc "applegamble","Clarice";
	end;
}

function	script	applegamble	{
	.@npc_name$ = getarg(0);
	switch (select("Jogar:Regras do Jogo:Cancelar")) {
	case 3:
		mes "["+getarg(0)+"]";
		mes "Eu estou pronta para jogar o jogo dos";
		mes "dados quando você quiser.";
		mes "Quando quiser jogar basta falar comigo, Ok?";
		close;
	case 2:
		mes "["+getarg(0)+"]";
		mes "As regras do Jogo dos Dados";
		mes "são bastante simples. Primeiro, você";
		mes "deve saber que as apostas são Maçãs.";
		mes "Você pode apostar no máximo 50";
		mes "Maçãs por vez. Para que tudo ocorra";
		mes "bem, eu aceito apenas Maçãs.";
		next;
		mes "["+getarg(0)+"]";
		mes "Mas ei, se você possuir tantos zenys";
		mes "que já não cabem mais em seus bolsos,";
		mes "tire-os e você pode comprar quantas";
		mes "maçãs quiser no Vendedor de Maçãs~";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, nós iremos começar rolando";
		mes "dois dados de 6 lados.";
		mes "Enquanto for seu turno, você poderá";
		mes "rolar dois dados de 6 lados. Após";
		mes "isso, nós teremos a opção";
		mes "de rolar um terceiro dado.";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora aqui é a parte importante.";
		mes "Se ao total nos dados você atingir 12,";
		mes "você irá a falência";
		mes "e irá perder. Caso contrário, a";
		mes "a pessoa com a maior pontuação";
		mes "irá vencer. O que acha?";
		next;
		mes "["+getarg(0)+"]";
		mes "Agora, primeiro você deve";
		mes "decidir se quer jogar o";
		mes "terceiro dado ou não. Então,";
		mes "dependendo do seu resultado, eu irei";
		mes "rolar meu terceiro dado... Ou talvez não.";
		next;
		mes "["+getarg(0)+"]";
		mes "Enquanto você ganhar, você irá";
		mes "receber o dobro de Maçãs";
		mes "que você apostou.";
		mes "Mas se por acaso você empatar, você irá";
		mes "receber de volta apenas as Maçãs";
		mes "que você apostou. Justo, certo?";
		close;
	case 1:
		break;
	}
	mes "["+getarg(0)+"]";
	mes "Ah, Então você quer jogar comigo?";
	mes "Ótimo! Quantas Maçãs";
	mes "você deseja apostar?";
	mes "Lembre-se, seu limite são 50 Maçãs.";
	mes "Se você deseja cancelar, por favor digite '0'.";
	next;
	while(1) {
		input .@amount;
		if (.@amount == 0) {
			mes "["+getarg(0)+"]";
			mes "Mudou de idéia?";
			mes "Eu entendo. Bem então,";
			mes "espero que possamos jogar depois.";
			close;
		}
		else if (.@amount < 1 || .@amount > 50) {
			mes "["+getarg(0)+"]";
			mes "Você não pode apostar mais do que";
			mes "50 Maçãs. Lembre-se,";
			mes "precisamos manter as apostas";
			mes "razoáveis. Por favor digite";
			mes "um valor não superior a 50.";
			next;
			continue;
		}
		mes "["+getarg(0)+"]";
		mes "Então você estará";
		mes "apostando ^FF0000"+.@amount+"^000000 Maçãs.";
		mes "Certo?";
		next;
		if (select("Yes:No") == 2) {
			mes "["+getarg(0)+"]";
			mes "Hum, cometi um erro?";
			mes "Certo, por favor digite";
			mes "o número de Maçãs que";
			mes "você deseja apostar.";
			next;
			continue;
		}
		if (countitem(512) <.@amount) {
			mes "Me desculpe, mas você";
			mes "não parecem ter";
			mes "Maçãs suficientes para essa";
			mes "aposta... Você não pode apostar";
			mes "se não tem a aposta, você sabe.";
			next;
			continue;
		}
		delitem 512,.@amount;
		mes "["+getarg(0)+"]";
		mes "Bom!";
		mes "Agora podemos começar";
		mes "este jogo! Irei rolar primeiro~";
		break;
	}
	mes "^3355FF*Rolando e caindo*^000000";
	next;
	set .@giveapple, .@amount*2;
	set .@table1, rand(1,6);
	set .@table2, rand(1,6);
	set .@tablesub, .@table1 + .@table2;
	set .@tabletotal, .@tablesub;
	mes "["+getarg(0)+"]";
	mes "Eu tenho um ^0000FF" + .@table1 + "^000000 e um ^0000FF" + .@table2 + "^000000.";
	mes "Isso é um total de ^0000FF" + .@tablesub + "^000000.";
	mes "^FF0000" + strcharinfo(0) + "^000000, agora é sua vez.";
	next;
	select("Lançar Dado.");
	mes "^3355FF*Rolando e caindo*^000000";
	set .@player1, rand(1,6);
	set .@player2, rand(1,6);
	set .@playersub, .@player1 + .@player2;
	if (.@playersub > 9 && .@amount > 39) {
		set .@player1, rand(1,6);
		set .@player2, rand(1,6);
		set .@playersub, .@player1 + .@player2;
	}
	set .@playertotal, .@playersub;
	next;
	mes "["+ .@npc_name$ +"]";
	mes "^FF0000" + strcharinfo(0) + "^000000, você tem ^FF0000" + .@player1 + "^000000 e ^FF0000" + .@player2 + "^000000. O total é ^FF0000" + .@playersub + "^000000 .";
	next;
	mes "["+ .@npc_name$ +"]";
	if(.@playersub == .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Estamos empatados, gostaria de lançar o dado novamente?";
	} else if (.@playersub > .@tablesub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. ^FF0000" + strcharinfo(0) + "^000000, atualmente você está ganhando o jogo, gostaria de lançar o dado novamente?";
	} else if(.@tablesub > .@playersub) {
		mes "Atualmente meu total é ^0000FF" + .@tablesub + "^000000 e ^FF0000" + strcharinfo(0) + "^000000, seu total é ^FF0000" + .@playersub + "^000000. Estou vencendo o jogo. Gostaria de lançar o dado novamente?";
	}
	next;
	switch (select("Lançar Dado.:Cancelar.")) {
	case 1:
		mes "^3355FF*Rolando e caindo*^000000";
		set .@player3, rand(1,6);
		set .@playertotal, .@playertotal + .@player3;
		next;
		mes "["+ .@npc_name$ +"]";
		if (.@playertotal > 12) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Você perdeu o jogo. Me desculpe mas, por favor tente novamente.";
			close;
		}
		else if (.@playertotal < .@tablesub) {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Mesmo que você lance o dado novamente, seu total ainda será menor do que o meu.  Você perdeu o jogo. Sinto muito e por favor tente novamente.";
			close;
		}
		else if (.@playertotal == .@tablesub) {
			if (.@tablesub > 8) {
				mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Eu não quero correr nenhum risco, vamos acabar este jogo num empate. Vamos jogar de novo outra hora~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Tudo bem.";
			mes "Deixe-me jogar os dados novamente.";
		}
		else {
			mes "^FF0000" + strcharinfo(0) + "^000000, você possui ^FF0000" + .@player3 + "^000000 e o total agora é ^FF0000" + .@playertotal + "^000000. Agora é minha vez.";
		}
		break;
	case 2:
		mes "["+ .@npc_name$ +"]";
		if (.@playersub > .@tablesub) {
			mes "Entendo, você não quer correr risco de perder o jogo. Ok, deixe-me lançar os dados novamente.";
		}
		else if (.@playersub == .@tablesub) {
			if (.@tablesub > 8) {
				mes "Entendo, você não quer correr risco de perder este jogo. Nem eu, vamos acabar este jogo em um empate. Vamos jogar novamente outra hora~";
				close2;
				getitem 512,.@amount; //Apple
				end;
			}
			mes "Certo.";
			mes "Vamos rolar os dados novamente.";
		}
		else {
			mes "Não faria mal tentar.";
			mes "Bem, eu venci dessa vez.";
			mes "Sinto muito, vamos tentar jogar";
			mes "novamente algum dia.";
			close;
		}
		break;
	}
	next;
	mes "^3355FF*Rolando e caindo*^000000";
	set .@table3, rand(1,6);
	set .@tabletotal, .@tabletotal + .@table3;
	next;
	mes "["+ .@npc_name$ +"]";
	if (.@tabletotal > 12) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Perdi o jogo por que meu total excedeu 12. Deixe-me dar-lhe minhas maçãs. Parabéns, foi um grande jogo.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal > .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 você ganhou este jogo, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe minhas maçãs. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@giveapple; //Apple
		end;
	}
	else if (.@playertotal == .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 este jogo acabou em empate, ^FF0000" + strcharinfo(0) + "^000000. Deixe-me dar-lhe suas maçãs de volta. Foi um grande jogo e espero que possamos jogar novamente em outro momento.";
		close2;
		getitem 512,.@amount; //Apple
		end;
	}
	else if (.@playertotal < .@tabletotal) {
		mes "Tenho ^0000FF" + .@table3 + "^000000 e o total agora é ^0000FF" + .@tabletotal + "^000000. Com o total de ^FF0000" + .@playertotal + "^000000 você perdeu o jogo, ^FF0000" + strcharinfo(0) + "^000000. Lamento, mas por favor, tente novamente.";
		close;
	}
}
